import org.gradle.internal.os.OperatingSystem

import java.time.Duration

plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    // check dependency updates: ./gradlew dependencyUpdates -Drevision=release
    id 'com.github.ben-manes.versions' version '0.41.0'
    id 'com.github.hierynomus.license' version '0.16.1'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = 'io.perfana'
version = file("VERSION").readLines().first()
description = 'test-events-loadrunner-cloud'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {

    ext.jacksonVersion="2.13.1"
    ext.httpClientVersion="4.5.13"
    ext.lombokVersion="1.18.22"

    implementation "com.github.stephenc.jcip:jcip-annotations:1.0-1"
    implementation "io.perfana:event-scheduler:3.0.2"
    implementation "org.apache.httpcomponents:httpclient:$httpClientVersion"
    implementation "org.apache.httpcomponents:httpmime:$httpClientVersion"
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    // for correct date time parsing in json
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    testImplementation "junit:junit:4.13.2"
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.32.0"
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    // enable logging in tests, e.g. to see HttpClient traffic, see test resource logback.xml
    testImplementation "org.slf4j:jcl-over-slf4j:1.7.35"
    testImplementation "ch.qos.logback:logback-classic:1.2.10"
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

license {
    // license check is broken on windows, skip
    if (OperatingSystem.current().isWindows()) ignoreFailures = true
    header = file("src/main/resources/META-INF/LICENSE.txt")
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Peter Paul Bakker, Perfana'
    excludes(["**/*.txt", "src/main/resources/META-INF/services/**"])
    // the standard java style makes intellij complain about dangling javadoc
    mapping('java', 'SLASHSTAR_STYLE')
    skipExistingHeaders true
}

if (project.hasProperty('release')) {

    subprojects {

        task javadocJar(type: Jar) {
            archiveClassifier.set('javadoc')
            from javadoc
        }

        task sourcesJar(type: Jar) {
            archiveClassifier.set('sources')
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }
        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { deployment -> signing.signPom(deployment) }
                    // Target repository
                    String repositoryUrl
                    if (version.endsWith("SNAPSHOT")) {
                        repositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                    }
                    else {
                        repositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    }
                    repository(url: repositoryUrl) {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.getName()
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/perfana/test-events-loadrunner-cloud.git'

                        scm {
                            connection 'scm:git:https://github.com/perfana/test-events-loadrunner-cloud.git'
                            developerConnection 'scm:git:git@github.com:perfana/test-events-loadrunner-cloud.git'
                            url 'https://github.com/perfana/test-events-loadrunner-cloud.git'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'peterpaulbakker'
                                name = 'Peter Paul Bakker'
                                email = 'peter.paul@perfana.io'
                            }
                        }
                    }
                }
            }
        }

    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
    // Prevent "java.net.SocketTimeoutException: timeout", when Sonatype is slow.
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}

// prevent alpha releases as a suggestion to update in dependencyUpdates
// see: https://github.com/ben-manes/gradle-versions-plugin#revisions
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}



